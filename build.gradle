buildscript {
    ext.kotlin_version = '1.2.0'
    ext.baseGroup="io.opencubes"
    ext.modId="boxlin"
    ext.modVersion="1.1.0"
    repositories {
        jcenter()
        maven { url "http://files.minecraftforge.net/maven" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.0.9"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'kotlin'
apply plugin: 'com.jfrog.bintray'
apply plugin: "com.matthewprenger.cursegradle"

version = modVersion
group = baseGroup + "." + modId
archivesBaseName = modId

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.1.2562"
    runDir = "run"

    mappings = "snapshot_20171003"
    makeObfSourceJar = false
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    testCompile 'junit:junit:4.4'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

repositories {
    jcenter()
    mavenCentral()
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

bintray {
    user = bintrayUser
    key = bintrayKey
    pkg {
        repo = "minecraft"
        name = "Boxlin"
        configurations = ['archives']
        licenses = ["MIT"]
        vcsUrl = "https://github.com/ocpu/Boxlin.git"

        version {
            name = modVersion
        }
    }
}

curseforge {
    apiKey = curseforgeKey // This should really be in a gradle.properties file
    project {
        id = '283350'
        changelog = 'Initial curseforge release' // A file can also be set using: changelog = file('changelog.txt')
        releaseType = 'release'
        changelogType = 'markdown'
    }
}
